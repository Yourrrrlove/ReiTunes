set shell := ["nu", "-c"]

watch:
    watchexec --exts=rs,sql,html,css,js --on-busy-update=restart -- cargo run -- --live-reload

deploy-local:
    cargo build --release
    mv -f target/release/reitunes-rs ~/bin/reitunes-rs
    reitunes-rs install
    systemctl --user daemon-reload
    systemctl --user restart reitunes

build-with-secrets:
    op run --env-file="./prod.env" -- cargo build

publish:
    op run --env-file="./prod.env" -- cross build --target x86_64-unknown-linux-musl --release
    rsync target/x86_64-unknown-linux-musl/release/reitunes spudnik.reillywood.com:bin/
    ssh spudnik.reillywood.com -t "systemctl --user restart reitunes"

run:
    cargo run

test:
    cargo nextest run

watch-tests:
    watchexec --exts=rs,sql,html -- cargo nextest run

expected_filename := if os_family() == "windows" { "reitunes-rs.exe" } else { "reitunes-rs" }

build-release:
    cargo build --release
    @$"Build size: (ls target/release/{{expected_filename}} | get size)"

publish-to-local-bin: build-release
    cp target/release/{{expected_filename}} ~/bin/

build-linux-x64:
    cross build --target x86_64-unknown-linux-gnu --release

build-linux-arm64:
    cross build --target aarch64-unknown-linux-gnu --release

build-windows-on-linux:
    cross build --target x86_64-pc-windows-gnu --release

test-api:
    #!/usr/bin/env nu
    let response = (curl -X POST -H "Content-Type: application/json" -H "X-API-Key: apikey" -d '{"file_path": "/test/path/file.mp3"}' http://localhost:5000/api/add)
    let status_code = $response.status
    if $status_code == 201 {
        echo "API test passed: Status code is CREATED (201)"
    } else {
        echo $"API test failed: Expected status code 201, but got ($status_code)"
        exit 1
    }
