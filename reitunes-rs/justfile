set shell := ["nu", "-c"]

watch:
    watchexec --exts=rs,sql,html,css --on-busy-update=restart -- cargo run

deploy-local:
    cargo build --release
    mv -f target/release/reitunes-rs ~/bin/reitunes-rs
    reitunes-rs install
    systemctl --user daemon-reload
    systemctl --user restart reitunes

run:
    cargo run

test:
    cargo nextest run

watch-tests:
    watchexec --exts=rs,sql,html -- cargo nextest run

expected_filename := if os_family() == "windows" { "reitunes-rs.exe" } else { "reitunes-rs" }

build-release:
    cargo build --release
    @$"Build size: (ls target/release/{{expected_filename}} | get size)"

publish-to-local-bin: build-release
    cp target/release/{{expected_filename}} ~/bin/

build-linux-x64:
    cross build --target x86_64-unknown-linux-gnu --release

build-linux-arm64:
    cross build --target aarch64-unknown-linux-gnu --release

build-windows-on-linux:
    cross build --target x86_64-pc-windows-gnu --release
